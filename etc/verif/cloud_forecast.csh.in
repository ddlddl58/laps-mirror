#!@CSH@

# First argument is $LAPS_DATA_ROOT

# Second argument is number of forecast time steps (excluding analysis)

# Third argument is domain name

# Fourth argument is subtype (comp or comp2 - optional)

setenv LAPSINSTALLROOT @prefix@
setenv LAPS_DATA_ROOT $1
setenv NLINE $2
setenv DOMAIN_NAME_IN $3
setenv SUBTYPE $4           

setenv VAR SWI
setenv VAR_LC swi
setenv TYPE pt

if ($SUBTYPE == "") then
    setenv SUBDIR $TYPE
    setenv DURATION ""
    setenv SUMMARY_FILE /dev/null
    echo "SUMMARY_FILE is $SUMMARY_FILE"
else
    setenv SUBDIR $TYPE/$SUBTYPE
    if($SUBTYPE == "comp") then
        setenv DURATION "7-day "
    else
        setenv DURATION "30-day "
    endif
    setenv SUMMARY_FILE $LAPS_DATA_ROOT/lapsprd/verif/$VAR/$TYPE/summary_$SUBTYPE.txt
    echo "SUMMARY_FILE is $SUMMARY_FILE"
    ls -l $SUMMARY_FILE
endif

echo "DURATION = $DURATION"

setenv DOMAIN_NAME `echo $DOMAIN_NAME_IN | sed 's/_/ /g'`

#setenv LAPS_DATA_ROOT /data/fab/projects/dwr
setenv LOGDIR $LAPS_DATA_ROOT/log

setenv MODELTIME      `head -1 $LAPS_DATA_ROOT/time/modelvtime.dat`
setenv MODELTIME_HHMM `head -1 $LAPS_DATA_ROOT/time/modelvtime.dat | cut -c6-9`

setenv MODEL_ASCIITIME `tail -1 $LAPS_DATA_ROOT/time/modelvtime.dat`

setenv FILE $LAPS_DATA_ROOT/lapsprd/verif/SWI/$SUBDIR/$MODELTIME.stats
setenv MEMBERS_FILE $LAPS_DATA_ROOT/lapsprd/verif/members.txt

setenv VERIF_INTVL `@PERL@ @prefix@/etc/read_nl.pl -d $LAPS_DATA_ROOT -n nest7grid.parms -v verif_intvl`
@ VERIF_FCST_LEN = ( $NLINE - 1 ) * $VERIF_INTVL
@ VERIF_FCST_LEN_HH = $VERIF_FCST_LEN / 3600
#setenv VERIF_FCST_LEN `@PERL@ @prefix@/etc/read_nl.pl -d $LAPS_DATA_ROOT -n nest7grid.parms -v model_fcst_len`
#setenv MODEL_FCST_LEN  `@PERL@ @prefix@/etc/read_nl.pl -d $LAPS_DATA_ROOT -n nest7grid.parms -v model_fcst_len`

# Setup True Type fonts and related environment variables
setenv GNUPLOT_DEFAULT_GDFONT VeraBd                

if (-d /usr/X11R6/lib/X11/fonts/truetype) then
    setenv GDFONTPATH /usr/X11R6/lib/X11/fonts/truetype
else if (-d /usr/share/fonts/bitstream-vera) then
    setenv GDFONTPATH /usr/share/fonts/bitstream-vera  
else if (-d /usr/share/fonts/dejavu) then
    setenv GDFONTPATH /usr/share/fonts/dejavu           
    setenv GNUPLOT_DEFAULT_GDFONT DejaVuSans-Bold            
else if (-d /usr/share/fonts/msttcore) then
    setenv GDFONTPATH /usr/share/fonts/msttcore
    setenv GNUPLOT_DEFAULT_GDFONT verdanab            
else
    setenv GDFONTPATH /dev/null
endif

setenv FONTFILE $GDFONTPATH/$GNUPLOT_DEFAULT_GDFONT.ttf

if (-e $FONTFILE) then
    echo "GNUPLOT FONT is $FONTFILE"                                    
#   setenv TERM_FONT 'enhanced font "'$GNUPLOT_DEFAULT_GDFONT', 12"'
    setenv TERM_FONT 'enhanced'
    setenv DEFAULT_FONT 'font "'$GNUPLOT_DEFAULT_GDFONT',12"'
    setenv TITLE_FONT 'font "'$GNUPLOT_DEFAULT_GDFONT',20"'
    setenv MEMBER_FONT 'font "'$GNUPLOT_DEFAULT_GDFONT',16"'
    if (-d /w3/lapb) then
        setenv KEY_WIDTH -19.0
    else
        setenv KEY_WIDTH -3 
    endif
    setenv KEY_HEIGHT 0.3
    setenv KEY_SPACING 0.7
    setenv TH "{/$GNUPLOT_DEFAULT_GDFONT=8 "
    setenv TF \}
else
    echo "FONTFILE $FONTFILE does not exist"
    setenv TERM_FONT ""                             
    setenv DEFAULT_FONT ""
    setenv TITLE_FONT ""
    setenv MEMBER_FONT ""
    setenv KEY_FONT ""
    setenv KEY_WIDTH 1
    setenv KEY_HEIGHT 0
    setenv KEY_SPACING 1.0
    setenv TH ""             
    setenv TF ""
endif

if ($SUMMARY_FILE != /dev/null && -e $SUMMARY_FILE) then # Percentage will be present
    setenv L_PLOT_CRITERIA `tail -1 $SUMMARY_FILE`
    setenv PCT_SUCCESS     `tail -1 $SUMMARY_FILE`
#   setenv L_PLOT_CRITERIA T  # for testing
    if ($KEY_WIDTH == 0) then # TrueType case
        setenv KEY_WIDTH -16 
    endif
else                                                     # Percentage will not be present
    setenv L_PLOT_CRITERIA T 
    if ($KEY_WIDTH == 0) then # TrueType case
        setenv KEY_WIDTH -14 
    endif
endif

echo " "
echo "inputs..."
echo "TYPE = $TYPE"  
echo "VAR = $VAR"  
echo "FILE = $FILE"
ls -l $FILE

@ NLINE = $NLINE + 2

# obtain xrange times from the data file
setenv STARTTIME1 `head -2      $FILE | tail -1 | cut -c2-12` ; echo $STARTTIME1
setenv STARTTIME2 `head -2      $FILE | tail -1 | cut -c14-24`; echo $STARTTIME2
setenv STOPTIME1  `head -$NLINE $FILE | tail -1 | cut -c1-12` ; echo $STOPTIME1
setenv STOPTIME2  `head -$NLINE $FILE | tail -1 | cut -c14-24`; echo $STOPTIME2

setenv NMEMBERS `grep -v persistence $MEMBERS_FILE | wc -l | cut -c1-2`
echo "NMEMBERS: $NMEMBERS"

# Construct gnuplot .gp file
#setenv GNUPLOTFILE $LOGDIR/cloud_fcst.gp

setenv GNUPLOTFILE $LAPS_DATA_ROOT/lapsprd/verif/$VAR/$SUBDIR/$VAR_LC\_$TYPE\_fcst.gp
setenv PNGDIR      $LAPS_DATA_ROOT/lapsprd/verif/$VAR/$SUBDIR/
mkdir -p $PNGDIR
setenv PNGFILE     $VAR_LC\_$TYPE\_fcst\_$MODELTIME.png

setenv DQUOTE \"
echo 'set terminal png '$TERM_FONT' size 1500,1000'                        > $GNUPLOTFILE
if ($DURATION == "") then 
    echo 'set xdata time'                                                    >> $GNUPLOTFILE
    echo 'set timefmt "%d-%b-%Y %H:%M:%S"'                                   >> $GNUPLOTFILE
    if ($VERIF_FCST_LEN >= 86400) then
        echo 'set format x "%d/%H"'                                          >> $GNUPLOTFILE
    else
        echo 'set format x "%H:%M"'                                          >> $GNUPLOTFILE
    endif
endif
echo 'set output '$DQUOTE$PNGDIR$PNGFILE$DQUOTE                          >> $GNUPLOTFILE

# time range must be in same format as data file
if ($DURATION == "") then 
    echo 'set xrange ['$DQUOTE$STARTTIME1' '$STARTTIME2$DQUOTE':'$DQUOTE$STOPTIME1' '$STOPTIME2$DQUOTE']' >> $GNUPLOTFILE
else
    echo 'set xrange [00:'$VERIF_FCST_LEN_HH']'                          >> $GNUPLOTFILE
endif
echo 'set yrange [0:1000]'                                               >> $GNUPLOTFILE
echo 'set grid'                                                          >> $GNUPLOTFILE
echo 'set datafile missing "-99.900"'                                     >> $GNUPLOTFILE
if ($DURATION == "") then
    echo 'set xlabel "Initialized '$MODEL_ASCIITIME' UTC"' $MEMBER_FONT textcolor lt 2 >> $GNUPLOTFILE
    echo 'set ylabel "GHI (W/m**2)"' $MEMBER_FONT textcolor lt 2 >> $GNUPLOTFILE
    echo 'set title "Global Horizontal Irradiance Observed vs Forecast '$DURATION'('$DOMAIN_NAME' domain)"' $TITLE_FONT >> $GNUPLOTFILE
else
#   echo 'set xlabel "Ending on '$MODEL_ASCIITIME' UTC"' $MEMBER_FONT textcolor lt 2 >> $GNUPLOTFILE
    echo 'set xlabel "Forecast Hours - Latest Run At '$MODEL_ASCIITIME' UTC"' $MEMBER_FONT textcolor lt 2       >> $GNUPLOTFILE 
    echo 'set ylabel "GHI (W/m**2)"' $MEMBER_FONT textcolor lt 2 >> $GNUPLOTFILE
    echo 'set title "GHI Observed vs Forecast '$DURATION'('$DOMAIN_NAME' domain)"' $TITLE_FONT >> $GNUPLOTFILE
endif
#echo 'set key left box'                                                  >> $GNUPLOTFILE
echo 'set key ins'                                                        >> $GNUPLOTFILE
echo 'set key box'                                                        >> $GNUPLOTFILE
echo 'set key spacing' $KEY_SPACING                                                                    >> $GNUPLOTFILE
echo 'set key height' $KEY_HEIGHT                                                                      >> $GNUPLOTFILE
echo 'set key width' $KEY_WIDTH                                                                        >> $GNUPLOTFILE

setenv T1 "Fcst Mean"
setenv T2 "Obs Mean"
setenv T3 "RMS (F-O)"


#echo 'plot '$DQUOTE$FILE$DQUOTE' using 1:3 title "Forecast" with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
#echo        $DQUOTE$FILE$DQUOTE' using 1:4 title "Observed" with linespoints lw 3 pt 5 ps 1, \' >> $GNUPLOTFILE
#echo        $DQUOTE$FILE$DQUOTE' using 1:5 title "RMSE"     with linespoints lw 3 pt 5 ps 1'    >> $GNUPLOTFILE

setenv IMEMBER 1
while ($IMEMBER <= $NMEMBERS)
    @ IBLOCK = $IMEMBER - 1

    @ PT = $IMEMBER + 4

    if ($PT == 7) then
        @ PT = 4
    endif

    setenv MEMBER `head -$IMEMBER $MEMBERS_FILE | tail -1`

    if ($SUMMARY_FILE != /dev/null && -e $SUMMARY_FILE) then # Percentage will be present
        setenv PCT `head -$IMEMBER $SUMMARY_FILE | tail -1`
        setenv TITLE "`echo $MEMBER | sed 's/_/ /g'` $PCT_SUCCESS/$PCT%"

        echo "PCT_SUCCESS being compared to threshold of 80 is: $PCT_SUCCESS"
        if ($PCT_SUCCESS >= 80) then
            setenv LS 1
            if (-d /w3/lapb) then
                setenv LW 3
            else
                setenv LW 1
            endif
        else 
            setenv LS 1
            setenv LW 1
        endif

    else                                                     # Percentage not present
        setenv PCT ""
        setenv TITLE `echo $MEMBER | sed 's/_/ /g'`
        setenv LS 1
        if (-d /w3/lapb) then
            setenv LW 3
        else
            setenv LW 2
        endif

    endif

    echo "IMEMBER MEMBER IBLOCK TITLE: $IMEMBER $MEMBER $IBLOCK $TITLE"

    if ($DURATION == "") then 
        setenv TIME 1
    else
        setenv TIME \(\$1+\$2/60\)
    endif

    if ($NMEMBERS == 1) then
        echo 'plot '$DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':3 title "'"$TH"$T1 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':4 title "'"$TH"$T2 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 2, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':5 title "'"$TH"$T3 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 3'    >> $GNUPLOTFILE
    else if ($IMEMBER == 1) then
        echo 'plot '$DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':3 title "'"$TH"$T1 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':4 title "'"$TH"$T2 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 2, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':5 title "'"$TH"$T3 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 3, \' >> $GNUPLOTFILE
    else if ($IMEMBER == $NMEMBERS) then
        echo '     '$DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':3 title "'"$TH"$T1 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':4 title "'"$TH"$T2 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 2, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':5 title "'"$TH"$T3 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 3'    >> $GNUPLOTFILE
    else
        echo '     '$DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':3 title "'"$TH"$T1 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 1, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':4 title "'"$TH"$T2 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 2, \' >> $GNUPLOTFILE
        echo        $DQUOTE$FILE$DQUOTE' index '$IBLOCK' using '$TIME':5 title "'"$TH"$T3 $TITLE$TF'" with linespoints lw '$LW' pt '$PT' ps 2 lt 3, \' >> $GNUPLOTFILE
    endif

    @ IMEMBER = $IMEMBER + 1
end

# Run gnuplot
gnuplot < $GNUPLOTFILE

echo " "
echo "outputs..."
ls -l $GNUPLOTFILE
cd $PNGDIR
ls -l $PNGDIR/$PNGFILE                          

rm -f          $VAR_LC\_$TYPE\_fcst.png
ln -s $PNGFILE $VAR_LC\_$TYPE\_fcst.png

rm -f          $VAR_LC\_$TYPE\_fcst\_$MODELTIME_HHMM.png
ln -s $PNGFILE $VAR_LC\_$TYPE\_fcst\_$MODELTIME_HHMM.png

#convert -resize 90x60! $PNGFILE $VAR_LC\_$TYPE\_fcst\_$MODELTIME_HHMM\_thumb.png

echo " "
echo "outputs (old)..."
cp $PNGFILE $LOGDIR/cloud_fcst.png
ls -l $LOGDIR/cloud_fcst.png

